### Controls ####
# on
# on 5
# off
# quit or end
# start or init
################################
# On Indefinitely: caff on
# On for 5 Minutes: caff on 5
# Off: caff off
# Quit App: caff quit or caff end
# Start App: caff start or caff init
################################
# If you turn caffeine on and it's not started it will
# start automatically for you first. No need to call
# caff start first.
### END ###

on AppRunning(appName)
	tell application "System Events" to set appNameIsRunning to exists (processes where name is appName)
	return appNameIsRunning
end AppRunning

to splitString(aString, delimiter)
	set retVal to {}
	set prevDelimiter to AppleScript's text item delimiters
	log delimiter
	set AppleScript's text item delimiters to {delimiter}
	set retVal to every text item of aString
	set AppleScript's text item delimiters to prevDelimiter
	return retVal
end splitString

on alfred_script(q)
	
	set appActive to AppRunning("Caffeine")
	set tmp to my splitString(q, " ")
	try
		set nm to item 2 of tmp
	on error
		set nm to "default"
	end try

	tell application "Caffeine"
		if "on" is in q then
			if not appActive then
				activate
			end if
			try
				(nm as real) div 1
				turn on for (nm * 60)
			on error
				turn on
			end try
		else if "start" is in q or "init" is in q then
			activate
		else if "quit" is in q or "end" is in q then
			quit
		else
			turn off
		end if
	end tell
	
end alfred_script